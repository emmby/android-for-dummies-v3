// Tell gradle this is an android application
apply plugin: 'com.android.application'

android {
    // Tell gradle what version of tools to use to build our app.
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    // Tell gradle various things that will get written into our
    // AndroidManifest when the app is compiled.
    defaultConfig {
        // Our app ID, should agree with what's in AndroidManifest.xml
        applicationId "com.dummies.tasks"

        // The minimum Android OS version our app requires in order to
        // run.  The Play store will not let users download the app if
        // they do not meet these minimum requirements.
        // If your minSdkVersion does not agree with your
        // compileSdkVersion, it's up to you to make sure you never call
        // any new functionality from your app that wasn't present on
        // your minSdkVersion.  For example, if your minSdkVersion is
        // set to KitKat 4.4, you must not use any functionality that
        // is related to Material Design (which was introduced in
        // Android L), otherwise your app will crash on KitKat phones.
        minSdkVersion 21

        // The target SDK.  This should generally be the same as your
        // compileSdkVersion.
        targetSdkVersion 21

        // The version of our app, expressed as a number.  Every time
        // you release a new version of your app, increment this number
        // by one.
        versionCode 2

        // The version name of our app.  This is what we typically
        // think of as "the version" of our app.  It can be things like
        // 1.0, 2.0, 2.0.1, 3.0-alpha-1, 4.0 (build 1023), etc.
        versionName "1.0"
    }

    buildTypes {
        // Special rules to run when we create a "release" version of
        // our app (which we'll do when we upload the app to the Play
        // store)
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        phone {
            buildConfigField 'boolean', 'SHOULD_USE_PALETTE', 'true'
        }
        tablet {
            buildConfigField 'boolean', 'SHOULD_USE_PALETTE', 'false'
        }
    }
}

// Libraries that our app will use
dependencies {
    // recyclerview, cardview, and palette are all google libraries
    // used to create Android L apps.
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:palette-v7:21.0.3'

    // Picasso is a library from Square that makes downloading images easy
    compile 'com.squareup.picasso:picasso:2.4.0'

    // For wearables
    compile 'com.google.android.gms:play-services-wearable:6.5.87'

    // For tv
    compile 'com.android.support:leanback-v17:21.0.3'
    
    // For location
    compile 'com.google.android.gms:play-services-location:6.5.87'

    wearApp project(':TasksWear')
}